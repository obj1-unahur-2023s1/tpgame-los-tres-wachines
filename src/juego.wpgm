import wollok.game.*
import jugador.*
import obstaculos.*
import tipos.*
import hud.*
import sonidos.*
import habitaciones.*
import minijuegos.*

program juego{
	
	game.title("Escapa de la Mansion")
	game.height(18)
	game.width(29)
	game.boardGround("pisoBase.png")
	game.addVisual(bordeNegro)
	
	var estaEnCartelFinal = false
	
	const pasilloPrincipal = new PasilloPrincipal(minijuego = new MinijuegoPasillo(), id = 10)
	const habitacion0 = new HabitacionIzq(minijuego = new MinijuegoCajasPlacas(),id = 0)
	const habitacion1 = new HabitacionIzq(minijuego = new MinijuegoPalancas(), id =1)
	const habitacion3 = new HabitacionDer(minijuego = new MinijuegoPalancas(), id =2)
	const habitacion2 = new HabitacionDer(minijuego = new MinijuegoCajasPlacas(), id =3)
	const habitaciones = [habitacion0,habitacion1,habitacion2,habitacion3]
	const cartelFinal = new CartelMuerte()
	
	var habitacionActual = pasilloPrincipal
	var posDeAlexEnPasillo

	pasilloPrincipal.mostrarVisuales()
	cantidadVida.texto(alex.getVidas())
		
	game.addVisual(cantidadVida)
	game.addVisual(visualVida)
	game.addVisual(alex)
	
	
	game.onCollideDo(alex, {objeto => 
		if(alex.colisionCon_DeTipo_(objeto, trampa)){
			objeto.activarTrampa()
			ouh.play()
			alex.recibirDanio()
			cantidadVida.actualizar(alex)
			game.schedule(500, {
				if(alex.getVidas() == 0){
					estaEnCartelFinal = true
					habitacionActual.esconderVisuales()
					game.addVisual(cartelFinal)
				}
				objeto.estadoInicial()
			})
			//---colision con puerta---
		}else if(alex.colisionCon_DeTipo_(objeto, puerta)){
			const puertaId = objeto.id()
			game.removeVisual(alex)
			habitacionActual.esconderVisuales()
			bordeNegro.image("NegroSalaHab"+alex.vistaActual()+".png")
			if(puertaId.between(0,1)){
				posDeAlexEnPasillo = objeto.position().right(1)
				habitacionActual = habitaciones.find({h=>h.id() == puertaId})
				alex.position(game.at(26,9))
			}else if(puertaId.between(2,3)){
				posDeAlexEnPasillo = objeto.position().left(1)
				habitacionActual = habitaciones.find({h=>h.id() == puertaId})
				alex.position(game.at(2,9))
			}else if(puertaId == 666){
				
			}else{
				bordeNegro.image("bordeNegroPasillo.png")
				alex.position(posDeAlexEnPasillo)
				habitacionActual = pasilloPrincipal
			}
			habitacionActual.mostrarVisuales()
			game.addVisual(alex)
			cantidadVida.actualizar(alex)
		}
	})
	
	keyboard.o().onPressDo({
		habitacionActual.esconderVisuales()
		habitacionActual = pasilloPrincipal
		habitacionActual.estadoInicial()
		habitaciones.forEach({h=>h.estadoInicial()})
		alex.position(game.center())
		alex.setVidas(3)
		cantidadVida.actualizar(alex)
		bordeNegro.image("bordeNegroPasillo.png")
		estaEnCartelFinal = false
	})
	
	keyboard.r().onPressDo({
		if(estaEnCartelFinal){
			habitacionActual = pasilloPrincipal
			habitacionActual.estadoInicial()
			habitaciones.forEach({h=>h.estadoInicial()})
			game.removeVisual(cartelFinal)
			alex.position(game.center())
			alex.setVidas(3)
			cantidadVida.actualizar(alex)
			bordeNegro.image("bordeNegroPasillo.png")
			estaEnCartelFinal = false
		}
	})
	
	keyboard.t().onPressDo({
		if(estaEnCartelFinal){
			game.stop()
		}
	})

	
	keyboard.w().onPressDo({alex.moverArriba()})
	keyboard.a().onPressDo({alex.moverIzquierda()})
	keyboard.s().onPressDo({alex.moverAbajo()})
	keyboard.d().onPressDo({alex.moverDerecha()})
	
	keyboard.e().onPressDo({
		if(!estaEnCartelFinal and alex.movimientoPermitido()){
			//---puerta---
			if(alex.hayObstaculo_Adelante(puerta)){
				var puertaAdelante = alex.obtenerObjetosAdelante().find({o => o.tipo() == puerta})
				if(!puertaAdelante.esAtravesable()){				
					puertaAdelante.abrirPuerta()
				}
				//---caja---
			}else{
				if(!alex.tieneLasManosOcupadas() and alex.hayColisionConObjetoTipo_oTipo_(caja,bloqueForma) and !alex.hayColisionConObjetoTipo_(posaObjeto)){
					var objetoManipulable
					if(alex.hayColisionConObjetoTipo_(caja)){
						objetoManipulable = alex.objetoDeTipo_EnColision(caja)
					}else{
						objetoManipulable = alex.objetoDeTipo_EnColision(bloqueForma)
					}
					alex.agarrarObjeto(objetoManipulable) 
					habitacionActual.minijuego().removerElemento(objetoManipulable)
					game.removeVisual(objetoManipulable)
					alex.actualizarVisual()
				}else if(alex.tieneLasManosOcupadas() and !alex.hayColisionConObjetoTipo_oTipo_(caja,bloqueForma)){
					const objetoManipulable = alex.soltarObjeto()
					habitacionActual.minijuego().agregarVisual(objetoManipulable)
					objetoManipulable.position(alex.position())
					game.addVisual(objetoManipulable)
					alex.actualizarVisual()
					if(alex.hayColisionConObjetoTipo_(posaObjeto)){
						const placa = alex.objetoDeTipo_EnColision(posaObjeto)
						placa.activar()
						habitacionActual.minijuego().recibirAccion(placa)
					}
				//---palanca---
				}else if(!alex.tieneLasManosOcupadas() and alex.hayColisionConObjetoTipo_(palanca)){
					const palancaActual = alex.objetoDeTipo_EnColision(palanca)
					if(!palancaActual.estaActiva()){						
						palancaActual.activar()
						habitacionActual.minijuego().recibirAccion(palancaActual)
					}
				}
				if(habitacionActual.minijuego().estaEnEstadoCritico()){
					alex.bloquearMovimiento()
					ouh.play()
					alex.recibirDanio()
					game.schedule(500, {
						if(alex.getVidas() == 0){
							estaEnCartelFinal = true
							habitacionActual.esconderVisuales()
							game.addVisual(cartelFinal)
						}
					})
				}
			} 
		}
		if(habitacionActual.minijuego().minijuegoCompletado() and habitacionActual.minijuego().minijuegoEstaActivo()){
			habitacionActual.minijuego().desactivarMinijuego()
			habitacionActual.abrirPuertas()
			var puertaEnPasillo = pasilloPrincipal.visuales().find({o => (o.tipo() == puerta and o.id() == habitacionActual.id())})
			puertaEnPasillo.bloquearPuerta() // mover dentro del codigo de la habitacion (?
			pasilloPrincipal.minijuego().nivel_Completado(habitacionActual.id())
			pasilloPrincipal.actualizarEstado()
			game.say(alex,"minijuego Finalizado")
		}
	})

	
	game.start()
}